buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE"
    }
}

plugins {
    id "java"
    id "com.github.samueltbrown.cucumber" version "0.9"
    id "idea"
    id "war"
    id "jacoco"
}

apply plugin: 'org.springframework.boot'

war {
    baseName = "modern"
    version = "HEAD"
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            "org.springframework.boot:spring-boot-starter-data-jpa:1.4.3.RELEASE",
            "org.springframework.boot:spring-boot-starter-mustache:1.4.3.RELEASE",
            "org.springframework.boot:spring-boot-starter-security:1.4.3.RELEASE",
            "org.springframework.boot:spring-boot-starter-web:1.4.3.RELEASE",
            "com.github.mjeanroy:springmvc-mustache:0.5.3",
            'org.glassfish.jersey.core:jersey-client:2.25',
            "mysql:mysql-connector-java:6.0.5",
            files("libs/moss-common-2.1.0.0.jar"),
            "org.projectlombok:lombok:1.16.12",
            "org.webjars:bootstrap:3.3.7-1",
            "org.webjars:jquery:3.1.1",
            "org.hibernate:hibernate-validator:5.3.4.Final",
            "commons-beanutils:commons-beanutils:1.9.3",
            "org.springframework.boot:spring-boot-devtools:1.4.2.RELEASE"
    )
    testCompile(
            "org.springframework.boot:spring-boot-starter-test:1.4.2.RELEASE",
            "junit:junit:4.12",
            "org.mockito:mockito-all:1.10.19",
            "info.cukes:cucumber-java:1.2.5",
            "info.cukes:cucumber-spring:1.2.5",
            "org.seleniumhq.selenium:selenium-java:3.0.1",
            "org.assertj:assertj-core:3.6.1",
            "com.nitorcreations:junit-runners:1.3",
            gradleTestKit()
    )

    cucumberRuntime "info.cukes:cucumber-java:1.2.5"
}

cucumber {
    jvmOptions {
        environment 'SPRING_PROFILES_ACTIVE', 'test'
        if (project.hasProperty("port")) {
            environment "SERVER_PORT", "$port"
        }
        if (project.hasProperty("sonar")) {
            def jacocoAgent = zipTree(configurations.jacocoAgent.singleFile).filter {
                it.name == "jacocoagent.jar"
            }.singleFile
            jvmArgs = ["-javaagent:$jacocoAgent=destfile=$buildDir/jacoco/cucumber.exec,append=false"]
        }
    }
}

bootRun {
    environment 'SPRING_PROFILES_ACTIVE', 'dev'
    if (project.hasProperty('port')) {
        environment 'SERVER_PORT', '$port'
    }
}

test {
    environment 'SPRING_PROFILES_ACTIVE', 'test'
    if (!project.hasProperty("sonar")){
        jacoco{
            enable false
        }
    }
}

task copyDependencyLibraries(type: Copy) {
    into "$buildDir/output/libs"
    from configurations.compile
}

task copyTestDependencyLibraries(type: Copy) {
    into "$buildDir/output/test-libs"
    from configurations.testCompile
}

if (project.hasProperty("sonar")) {
    classes.dependsOn(copyDependencyLibraries)
    testClasses.dependsOn(copyTestDependencyLibraries)
}

task mergeUnitTestResults << {
    fileTree("${buildDir}/test-results")
            .matching { include '**/*$*.xml' }
            .getFiles()
            .each {
        def document = (new XmlParser()).parse(it)
        document.@name = document.@name.substring(0, document.@name.indexOf('$'))
        new XmlNodePrinter(new PrintWriter(it)).print(document)
    }
}

jacocoTestReport.dependsOn(mergeUnitTestResults)

